0 :
1 POKE 29943,255
2 DIM MS(6,9),MA$(10),MN$(10),SP(3,2,2),SP$(3,3)
3 DIM WP$(3,16),WP(3,11,1),ZZ$(3),ZY(2,2),ZS(3),WM$(10)
4 DIM H$(1),DR$(10),DR(10,4),DT$(10),HT$(1),TJ$(1,3)
5 GRAPH:GOSUB 700
301 FOR R=0 TO 40:LINE 1,R,160,R:LINE 1,80-R,160,80-R:NEXT 
302 FOR R=0 TO 10:LINE 1,40-R,160,40-R,0:LINE 1,40+R,160,40+R,0:NEXT 
303 FOR R=1 TO 1200:NEXT :LOCATE 3,6:PRINT "寻找朱诗荟";:R$=INKEY$
305 CLS:S=2:LOCATE 2,7:PRINT "开始游戏";:LOCATE 3,7:PRINT "读取进度";
306 LOCATE 4,7:PRINT "关于作者";:BOX 39,15,120,64: GOTO 312
310 BOX 40,16*T-16,119,16*T-1,1,2
312 BOX 40,16*S-16,119,16*S-1,1,2:T=S
315 R=ASC(INKEY$)
318 IF R=20 AND S>2 THEN S=S-1: GOTO 310
320 IF R=21 AND S<4 THEN S=S+1: GOTO 310
321 IF R=13 THEN  GOTO 325
322 IF R=27 THEN POKE 199,255
324 GOTO 315
325 IF S=2 THEN 990 ELSE IF S=3 THEN 7730
330 CLS:LOCATE 1,2:PRINT "剧情设计: 太乐&月萧";:LOCATE 2,2:PRINT "代码编写: 月萧";
332 LOCATE 3,2:PRINT "友情帮助: 柱子";:LOCATE 4,2:PRINT "测试人: 曳波";
333 LOCATE 5,2:PRINT "感谢广大Fans的支持";:R$=INKEY$: GOTO 305 
700 G=2000:M=1:X=4:Y=3:XX=4:YY=3:ZB$(0)="布衣":ZB$(1)="布衣"
701 HP(0)=80:MP(0)=5:QJ=1:N=1:FA=1:FB=1:FC=232
790 FOR R=0 TO 3: FOR S=1 TO 11:WP$(R,S)=" ":NEXT S,R
802 HP(1)=80:MP(1)=5:WQ$(0)="小刀":WQ$(1)="小刀":UC(0)=5:UC(1)=5:VD(0)=5:VD(1)=5:RETURN
990 CLS:S=7:OPEN "KS" FOR INPUT AS#2:GOSUB 6010:CLOSE #2
1000 CLS:LOCATE 3,6:PRINT "Loading...";: GOTO 6800
1002 FOR Q=0 TO 1:HM(Q)=80+5*A(Q):E(Q)=INT(80+0.4*A(Q)):C(Q)=30+2*A(Q):D(Q)=20+INT(1.5*A(Q))
1003 MM(Q)=5+A(Q):F(Q)=INT(10+0.1*A(Q)):NEXT 
1004 XX=X:YY=Y
1009 OPEN "CI"+STR$(N) FOR INPUT AS#1
1010 FOR R=1 TO 10
1012 INPUT #1,ZD(R):IF ZD(R)=0 THEN 1020
1015 INPUT #1,DR$(R),DT$(R),DR(R,0),DR(R,1),DR(R,2),DR(R,3),DR(R,4)
1020 INPUT #1,MN$(R)
1022 FOR S=0 TO 3:INPUT #1,FX(R,S):NEXT S
1025 INPUT #1,MA$(R)
1040 NEXT R
1047 IF EOF(1)=1 THEN 1200
1075 FOR R=0 TO 3
1076 INPUT #1,SP$(R,3)
1077 FOR S=0 TO 2:INPUT #1,SP$(R,S),SP(R,S,0),SP(R,S,1),SP(R,S,2)
1080 NEXT S,R
1090 INPUT #1,CQ,CS$(0),CD(0),CS$(1),CD(1)
1200 CLOSE #1
1405 CLS:LOCATE 1,1:PRINT MA$(M);
1407 FOR R=1 TO 5
1408 FOR S=1 TO 8
1409 MS(R,S)=ASC(MID$(MA$(M),20*R+2*S-20,1))
1410 NEXT S,R
1412 GOTO 1520
1505 CLS:LOCATE 1,1:PRINT MA$(M);
1520 FOR R=2 TO 4:LOCATE R,18:PRINT MID$(MN$(M),R*2-3,2);:NEXT R
1550 BOX 130,1,157,80
1555 LOCATE 1,1:PRINT "";
1690 LOCATE X,Y*2-1:PRINT "";
1700 IF CF=1 THEN 6200
1705 R=ASC(INKEY$)
1710 IF R=20 THEN XX=X-1: GOTO 1760
1720 IF R=21 THEN XX=X+1: GOTO 1760
1730 IF R=22 THEN YY=Y+1: GOTO 1760
1740 IF R=23 THEN YY=Y-1: GOTO 1760
1751 IF R=29 THEN ZD=0:GOSUB 3500: GOTO 1505
1753 IF R=28 THEN 4139
1754 IF R=30 THEN 1000
1755 IF R=31 THEN S=2: GOTO 7600
1760 IF XX*YY*(6-XX)*(9-YY)=0 THEN 2500
1761 L=MS(XX,YY)
1769 IF L<>0 AND L<>32 THEN 1801
1770 LOCATE X,Y*2-1:PRINT "  ";
1780 LOCATE XX,YY*2-1:PRINT "";
1790 X=XX:Y=YY
1792 IF ZD(M)=1 AND RND(1)<0.1 THEN 4500
1800 GOTO 1900
1801 IF L>177 AND L<193 AND L<>183 AND L<>187 THEN 7300
1802 IF L=212 THEN 3100
1803 IF L=215 THEN 3200
1804 IF L=220 THEN 3300
1805 IF L=216 THEN 3400
1806 IF L=224 THEN 5700
1807 IF L=229 THEN 5900
1808 IF FC=L AND FA=M AND FB=N THEN CF=1
1810 IF QJ>38 AND L=217 AND N=9 AND M=10 THEN N=11:M=1:X=4:XX=4:Y=4:YY=4: GOTO 1000
1815 IF L=162 AND N=11 AND M=1 THEN N=9:M=10:X=4:XX=4:Y=7:YY=7: GOTO 1000
1890 XX=X:YY=Y
1900 GOTO 1700
2500 IF X=1 THEN X=5:M=FX(M,0): GOTO 2510
2502 IF X=5 THEN X=1:M=FX(M,1): GOTO 2510
2504 IF Y=1 THEN Y=8:M=FX(M,2): GOTO 2510
2506 IF Y=8 THEN Y=1:M=FX(M,3): GOTO 2510
2510 XX=X:YY=Y:IF M<11 THEN 1405
2525 M=M-10:N=INT((N-1)/2)*4-N+3: GOTO 1000
3000 CLS
3002 FOR R=1 TO 30
3003 LINE 1,R,159,R
3004 LINE 1,80-R,159,80-R
3006 NEXT R
3010 LOCATE 3,6:PRINT SP$(Q,3):YY$=INKEY$
3011 CLS:LOCATE 1,5:PRINT ZX$;:LOCATE 1,14:PRINT "价格";:LOCATE 5,10:PRINT "现有:";G;
3013 FOR R=0 TO 2
3014 LOCATE R+2,5:PRINT SP$(Q,R);:LOCATE R+2,14:PRINT SP(Q,R,2);
3015 NEXT R
3016 LOCATE 5,5:PRINT "取消";:LOCATE S,3:PRINT "";:SS=S
3020 R=ASC(INKEY$)
3021 IF R=20 AND S>2 THEN SS=S-1: GOTO 3030 
3022 IF R=21 AND S<5 THEN SS=S+1: GOTO 3030 
3023 IF R=13 THEN 3045 
3024 IF R=27 THEN 1505 
3029 GOTO 3040 
3030 LOCATE S,3:PRINT "  ";:LOCATE SS,3:PRINT "";:S=SS
3040 GOTO 3020
3045 IF S=5 THEN ENT=0: GOTO 1505
3046 CLS:ENT=1:R=11-LEN(SP$(Q,S-2))/2
3048 LOCATE 1,R:PRINT SP$(Q,S-2);:LOCATE 2,4:PRINT ZP$;:LOCATE 3,4:PRINT ZQ$
3049 LOCATE 4,5:PRINT "价格:";:LOCATE 2,13:PRINT SP(Q,S-2,0)
3050 LOCATE 3,13:PRINT SP(Q,S-2,1)
3052 LOCATE 4,13:PRINT SP(Q,S-2,2)
3055 LOCATE 5,4:PRINT "是否购买?(Y/N)";
3056 YY$=INKEY$
3057 IF YY$="n" THEN  GOTO 3011
3058 IF YY$="y" THEN 3060
3059 GOTO 3056
3060 IF G>=SP(Q,S-2,2) THEN 3063
3061 CLS:LOCATE 2,8:PRINT "对不起";
3062 LOCATE 3,6:PRINT "你的钱不够":YY$=INKEY$: GOTO 3011
3063 IF ZS(Q)<10 THEN  GOTO 3067
3064 CLS:LOCATE 2,8:PRINT "对不起";
3065 LOCATE 3,4:PRINT "你的";ZR$;"已满";:YY$=INKEY$: GOTO 3011
3067 ZS(Q)=ZS(Q)+1:G=G-SP(Q,S-2,2):WP$(Q,ZS(Q))=SP$(Q,S-2)
3068 WP(Q,ZS(Q),0)=SP(Q,S-2,0):WP(Q,ZS(Q),1)=SP(Q,S-2,1)
3069 CLS:LOCATE 3,7:PRINT "交易成功":YY$=INKEY$: GOTO 3011
3100 Q=0:ZX$="药品":S=2:XX=X:YY=Y:ZP$="补HP:":ZQ$="补MP:":ZR$="医药箱"
3103 GOTO 3000
3200 Q=1:ZX$="暗器":S=2:XX=X:YY=Y:ZP$="杀伤力:":ZQ$="命中率:"
3201 ZR$="暗器箱"
3203 GOTO 3000
3300 Q=2:ZX$="武器":S=2:XX=X:YY=Y:ZP$="攻击力:":ZQ$="命中率:"
3301 ZR$="武器箱"
3303 GOTO 3000
3400 Q=3:ZX$="装备":S=2:XX=X:YY=Y:ZP$="防御力:":ZQ$="回避率:"
3401 ZR$="装备箱"
3403 GOTO 3000
3500 S=1
3501 CLS:LOCATE 2,5:PRINT "物";:LOCATE 4,5:PRINT "品";
3502 LOCATE 1,10:PRINT "药 品";
3503 LOCATE 2,10:PRINT "暗 器";
3504 LOCATE 3,10:PRINT "武 器";
3505 LOCATE 4,10:PRINT "装 备";
3506 LOCATE 5,10:PRINT "取 消";
3508 SS=S:BOX 64,S*16-16,119,S*16-1,1,2
3510 R=ASC(INKEY$)
3520 IF R=20 AND S>1 THEN SS=S-1: GOTO 3530
3522 IF R=21 AND S<5 THEN SS=S+1: GOTO 3530
3523 IF R=13 THEN  GOTO 3535
3524 IF R=27 THEN  GOTO 3540
3525 GOTO 3510
3530 BOX 64,S*16-16,119,S*16-1,1,2
3531 BOX 64,SS*16-16,119,SS*16-1,1,2
3532 S=SS: GOTO 3510
3535 IF S=1 THEN 3550
3536 IF S=2 THEN 3650
3537 IF S=3 THEN 3750
3538 IF S=4 THEN 3850
3539 IF S=5 THEN 3540
3540 RETURN
3550 XH=0:T=1:ZX$="药品":ZP$="补HP:":ZQ$="补MP:":SY=0
3558 GOSUB 3900
3559 IF SY=1 THEN SY=0: GOTO 3566
3564 GOTO 3501
3566 IF ZD=1 THEN 3570
3568 Q=0:GOSUB 4100:IF QQ=1 THEN 3558
3570 HP(Q)=HP(Q)+WP(XH,T,0):IF HP(Q)>HM(Q) THEN HP(Q)=HM(Q)
3571 MP(Q)=MP(Q)+WP(XH,T,1):IF MP(Q)>MM(Q) THEN MP(Q)=MM(Q)
3575 CLS:LOCATE 3,7:PRINT "使用完毕";
3577 FOR QQ=T TO ZS(XH)-1:WP$(XH,QQ)=WP$(XH,QQ+1):WP(XH,QQ,0)=WP(XH,QQ+1,0)
3578 WP(XH,QQ,1)=WP(XH,QQ+1,1):NEXT 
3579 IF T=ZS(XH) THEN T=T-1
3590 ZS(XH)=ZS(XH)-1:R$=INKEY$: GOTO 3558
3650 XH=1:T=1:ZX$="暗器":ZP$="杀伤力:":ZQ$="命中率:":SY=0
3658 GOSUB 3900
3659 IF SY=1 AND ZD=1 THEN 3670
3660 IF SY=1 THEN CLS:LOCATE 3,7:PRINT "无法使用":R$=INKEY$
3663 GOTO 3501
3670 AA=T
3680 RETURN
3750 XH=2:T=1:ZX$="武器":ZP$="攻击力:":ZQ$="命中率:":SY=0
3757 GOSUB 3900
3758 IF ZD=1 THEN 3501
3759 IF SY=1 THEN SY=0: GOTO 3780
3763 GOTO 3501
3780 GOSUB 4200
3781 IF JH=0 THEN 3785
3782 SWAP WQ$(Q),WP$(XH,T)
3783 SWAP UC(Q),WP(XH,T,0)
3784 SWAP UE(Q),WP(XH,T,1)
3785 GOTO 3757
3850 XH=3:T=1:ZX$="装备":ZP$="防御力:":ZQ$="回避率:":SY=0
3857 GOSUB 3900
3858 IF ZD=1 THEN 3501
3859 IF SY=1 THEN SY=0: GOTO 3880
3863 GOTO 3501
3880 GOSUB 4200
3881 IF JH=0 THEN 3885
3882 SWAP ZB$(Q),WP$(XH,T)
3883 SWAP VD(Q),WP(XH,T,0)
3884 SWAP VF(Q),WP(XH,T,1)
3885 GOTO 3857
3900 IF ZD=0 THEN 3950
3910 IF ZX$="药品" OR ZX$="暗器" THEN 3950
3920 CLS:LOCATE 2,8:PRINT "战斗中";:LOCATE 3,5:PRINT "无法更换";ZX$;:R$=INKEY$
3930 RETURN
3950 IF ZS(XH)=0 THEN 4000
3951 CLS:LOCATE 1,7:PRINT ZX$;:LOCATE 3,5:PRINT ZP$;:LOCATE 1,13:PRINT T;
3952 LOCATE 4,5:PRINT ZQ$;
3953 LOCATE 5,4:PRINT "F1使用  F2丢弃";
3954 LOCATE 2,5:PRINT "        ";
3955 LOCATE 2,7:PRINT "        ";:R=11-LEN(WP$(XH,T))/2:LOCATE 2,R:PRINT WP$(XH,T);
3956 LOCATE 1,13:PRINT "  ";:LOCATE 1,13:PRINT T;
3957 LOCATE 3,13:PRINT "       ";:LOCATE 3,13:PRINT WP(XH,T,0);
3958 LOCATE 4,13:PRINT "        ";:LOCATE 4,13:PRINT WP(XH,T,1);
3960 R=ASC(INKEY$)
3962 IF R=22 AND T<ZS(XH) THEN T=T+1: GOTO 3970
3963 IF R=23 AND T>1 THEN T=T-1: GOTO 3970
3964 IF R=27 THEN 4005
3965 IF R=28 THEN SY=1: GOTO 4005
3966 IF R=29 THEN  GOTO 3975
3969 GOTO 3960
3970 GOTO 3954
3975 CLS:LOCATE 3,4:PRINT "是否丢弃?(Y/N)"
3976 R$=INKEY$
3977 IF R$="y" THEN 3980
3978 IF R$="n" THEN 3951
3979 GOTO 3976 
3980 FOR QQ=T TO ZS(XH)-1:WP$(XH,QQ)=WP$(XH,QQ+1):WP(XH,QQ,0)=WP(XH,QQ+1,0)
3981 WP(XH,QQ,1)=WP(XH,QQ+1,1):NEXT 
3982 IF T=ZS(XH) THEN T=T-1
3983 ZS(XH)=ZS(XH)-1:IF ZS(XH)=0 THEN  GOTO 4000 ELSE  GOTO 3951
4000 CLS:LOCATE 3,7:PRINT "无";ZX$;
4001 R$=INKEY$
4005 RETURN
4100 QQ=0:CLS
4101 LOCATE 2,9:PRINT "太  乐";:LOCATE 4,9:PRINT "月  萧";
4102 LOCATE Q*2+2,7:PRINT "";
4115 R=ASC(INKEY$)
4116 IF R=20 AND Q=1 THEN Q=0
4117 IF R=21 AND Q=0 THEN Q=1
4118 IF R=27 THEN QQ=1: GOTO 4135
4119 IF R=13 THEN 4135
4120 LOCATE 4-Q*2,7:PRINT "  ";
4125 LOCATE Q*2+2,7:PRINT "";
4130 GOTO 4115
4135 RETURN
4139 Q=0
4140 GOSUB 4100
4141 IF QQ=1 THEN 1505
4142 CLS:LOCATE 1,8:PRINT H$(Q);:LOCATE 2,5:PRINT "等级: ";A(Q);
4143 LOCATE 3,5:PRINT "经验: ";B(Q);
4144 LOCATE 5,5:PRINT "MP: ";MP(Q);"/";MM(Q);
4145 LOCATE 4,5:PRINT "HP: ";HP(Q);"/";HM(Q);
4146 R$=INKEY$
4147 CLS
4148 CLS:LOCATE 1,5:PRINT "金钱: ";G;
4155 LOCATE 2,5:PRINT "攻击力: ";C(Q)+UC(Q);:LOCATE 3,5:PRINT "防御力: ";D(Q)+VD(Q);
4156 LOCATE 4,5:PRINT "武器: ";WQ$(Q);:LOCATE 5,5:PRINT "装备: ";ZB$(Q);
4157 R=ASC(INKEY$): GOTO 4140 
4200 CLS:LOCATE 3,4:PRINT "更换";ZX$;"?(Y/N)";
4210 R$=INKEY$
4220 IF R$="y" THEN 4250
4230 IF R$="n" THEN JH=0: GOTO 4280
4235 GOTO 4210 
4250 Q=0:GOSUB 4100
4260 IF QQ=1 THEN 4280 
4265 JH=1 
4270 CLS:LOCATE 3,7:PRINT "更换成功":R$=INKEY$
4280 RETURN
4500 DH$=DR$(M):DT$=DT$(M):DC=DR(M,0):DD=DR(M,1):DHM=DR(M,2):DHP=DHM:DJY=DR(M,3):DG=DR(M,4)
4503 Q=1:SY=0
4504 CLS:LOCATE 3,9-LEN(DR$(M))/2:PRINT "遇到";DR$(M);
4505 R=ASC(INKEY$)
4510 GOSUB 4608: GOTO 1505
4520 CLS:LOCATE 3,7:PRINT "战斗胜利";
4521 FOR R=1 TO 2000:NEXT R
4522 R=ASC(INKEY$)
4523 R=INT(DJY*(0.75+RND(1)/2)):B(0)=B(0)+R
4524 R=INT(DJY*(0.75+RND(1)/2)):B(1)=B(1)+R
4530 CLS:LOCATE 1,9:PRINT "获得"
4535 LOCATE 2,4:PRINT "经验系数   ";DJY;
4536 R=INT(DG*(0.8+RND(1)/2.5))
4540 LOCATE 4,4:PRINT "金钱     ";R;
4541 G=G+R:R$=INKEY$
4542 FOR Q=0 TO 1
4543 S=A(Q)+1:R=INT(100*(1.2^S-1))
4544 WHILE B(Q)>R
4545 S=S+1:R=INT(100*(1.2^S-1))
4546 WEND
4547 S=S-1:IF A(Q)=S THEN 4560
4551 CLS:LOCATE 3,7:PRINT H$(Q);"升级";:R$=INKEY$
4553 A(Q)=S:C(Q)=30+INT(1.4*S)
4554 D(Q)=20+INT(1.2*S)
4555 HM(Q)=80+5*S:HP(Q)=HM(Q):E(Q)=INT(65+0.5*S)
4556 MM(Q)=5+S:MP(Q)=MM(Q):F(Q)=INT(10+0.5*S)
4560 NEXT Q 
4565 RETURN
4600 DATA 4,33,4,38,8,33,8,38,5,35,7,35,10,33,10,38,11,33,13,33,11,36,13,36
4602 DATA 14,34,14,35,4,41,4,46,8,41,8,46,5,42,5,43,7,42,7,43,6,44,6,45
4604 DATA 10,41,10,46,11,41,13,41,11,44,13,44,14,42,14,43
4608 RESTORE:Q=1-Q 
4610 IF HP(Q)=0 THEN Q=1-Q
4616 CLS:LOCATE 1,3:PRINT H$(Q);:LOCATE 1,13:PRINT DH$;
4618 LOCATE 2,3:PRINT HT$(Q);:LOCATE 2,13:PRINT DT$;
4620 FOR R=1 TO 16
4630 READ S,T,QQ,SS
4640 LINE S,T,QQ,SS
4645 IF T<40 THEN LINE S+80,T,QQ+80,SS
4647 NEXT R
4651 LOCATE 5,3:PRINT "攻击";:LOCATE 5,15:PRINT "物品";
4652 LOCATE 5,9:PRINT "特技";:LOCATE 1,1:PRINT "";
4653 T=1:S=1
4660 BOX 16,34,55/HM(Q)*HP(Q)+16,37,1,1
4661 BOX 96,34,55/DHM*DHP+96,37,1,1
4663 BOX 16,39,71,39,1,1
4664 BOX 96,39,151,39,1,1
4665 BOX 16,42,55/MM(Q)*MP(Q)+16,45,1,1
4666 BOX 16,47,71,47,1,1
4700 LOCATE 4,3:PRINT "选择         ";
4704 IF BB>0 THEN  GOTO 4910
4705 IF AA>0 THEN GOSUB 5500: GOTO 4918
4706 BOX 48*S-34,64,48*S+1,79,1,2
4710 R=ASC(INKEY$)
4720 IF R=23 AND S>1 THEN T=S-1: GOTO 4750
4730 IF R=22 AND S<3 THEN T=S+1: GOTO 4750
4732 IF R=13 THEN 4800
4735 if r=122 goto 5300
4740 GOTO 4710
4750 BOX 48*T-34,64,48*T+1,79,1,2
4755 BOX 48*S-34,64,48*S+1,79,1,2
4760 S=T: GOTO 4710
4800 IF S=1 THEN BB=0: GOTO 4900
4810 IF S=2 THEN 7000
4830 IF S=3 THEN BB=0: GOTO 5400
4900 LOCATE 4,3:PRINT H$(Q);"攻击     ";
4901 LOCATE 5,2:PRINT " 攻击 ";
4902 FOR R=1 TO 1500:NEXT R
4903 R=1-RND(1)-(E(Q)+UE(Q))/100
4904 IF R<0 THEN 4910
4906 LOCATE 4,3:PRINT "没有击中       ";
4908 FOR R=1 TO 1500:NEXT R
4909 GOTO 4990 
4910 LOCATE 4,3:PRINT DH$;"受伤了     ";
4912 IF BB=0 THEN 4915
4913 R=BB:MP(Q)=MP(Q)-CC:BOX 16,42,71,45,1,0:BOX 16,42,55/MM(Q)*MP(Q)+16,45,1,1:BB=0
4914 GOTO 4918
4915 R=INT((C(Q)+UC(Q)-DD)*(RND(1)+1.5)):IF R<1 THEN R=2
4918 DHP=DHP-R
4919 IF R=0 THEN 4980
4920 FOR R=1 TO 1500:NEXT R
4930 IF DHP<1 THEN DHP=0
4961 BOX 96,34,151,37,1,0:IF DHP=0 THEN 4980
4970 BOX 96,34,96+55/DHM*DHP,37,1,1
4980 FOR R=1 TO 1500:NEXT R
4985 IF DHP=0 THEN 5300
4990 LOCATE 4,3:PRINT DH$;"攻击     ";
4995 FOR R=1 TO 1500:NEXT R
5000 R=RND(1)-(F(Q)+VF(Q))/100
5003 IF R>0 THEN 5050
5006 LOCATE 4,3:PRINT "没有击中       ";
5010 FOR R=1 TO 1500:NEXT R
5020 GOTO 5290
5050 LOCATE 4,3:PRINT H$(Q);"受伤了     ";
5060 R=INT((DC-D(Q)-VD(Q))*(RND(1)+1.5)):IF R<1 THEN R=2
5068 HP(Q)=HP(Q)-R:IF HP(Q)<1 THEN HP(Q)=0
5070 FOR R=1 TO 1500:NEXT R
5200 BOX 16,34,71,37,1,0:IF HP(Q)=0 THEN 5230
5220 BOX 16,34,55/HM(Q)*HP(Q)+16,37,1,1
5230 FOR R=1 TO 1500:NEXT R
5235 IF HP(Q)>0 THEN 5290
5236 CLS:LOCATE 3,7:PRINT H$(Q);"昏迷";:R$=INKEY$
5238 IF HP(0)+HP(1)=0 THEN 5310
5290 GOTO 4608
5300 GOTO 4520
5310 GOTO 7860
5400 AA=0:ZD=1:GOSUB 3500
5410 Q=1-Q
5420 GOTO 4608
5500 LOCATE 4,3:PRINT H$(Q);"投掷";WP$(1,AA);"   ";
5510 FOR R=1 TO 1500:NEXT R
5520 R=RND(1)-WP(1,AA,1)/100
5530 IF R<0 THEN 5600
5540 LOCATE 4,3:PRINT "没有击中         ";
5560 R=0
5570 GOTO 5630
5600 R=INT(WP(1,AA,0)*(RND(1)/4+1))
5610 LOCATE 4,3:PRINT DH$;"受伤了       ";
5630 FOR T=AA TO ZS(1)-1
5640 WP$(1,T)=WP$(1,T+1):WP(1,T,0)=WP(1,T+1,0):WP(1,T,1)=WP(1,T+1,1)
5650 NEXT T
5660 AA=0:ZS(1)=ZS(1)-1
5670 RETURN
5700 CLS:LOCATE 2,7:PRINT "欢迎光临";:LOCATE 3,6:PRINT MID$(MN$(M),1,4);"大赌场";
5701 XX=X:YY=Y:R$=INKEY$
5705 IF G<CQ*0.61 THEN CLS:LOCATE 3,6:PRINT "你的钱不够":R$=INKEY$: GOTO 1505
5710 CLS:DB$(0)="":DB$(1)="":DB$(2)="":DB$(3)="":DB$(4)="":R=-1 
5715 POKE 199,240:DB$(5)="":DB$(6)="":DB$(7)=""
5716 FOR Q=0 TO 2
5720 R=R+1:IF R=8 THEN R=0
5725 FOR SS=1 TO 250:NEXT 
5730 LOCATE 3,4*Q+6:PRINT DB$(R);
5750 IF PEEK (199)=141 THEN DB(Q)=R: GOTO 5770
5760 GOTO 5720 
5770 POKE 199,240
5775 NEXT Q
5780 R$=INKEY$:R$=INKEY$:IF DB(0)=DB(1) AND DB(0)=DB(2) THEN R=0.8+RND(1)/5: GOTO 5800
5790 IF DB(0)=DB(1) OR DB(0)=DB(2) OR DB(1)=DB(2) THEN R=0.2+RND(1)/10: GOTO 5800
5795 R=-0.4-RND(1)/5
5800 CLS:LOCATE 3,3:PRINT "获得金钱:";INT(R*CQ);
5815 R$=INKEY$:G=G+INT(CQ*R): GOTO 1505
5900 XX=X:YY=Y:CLS:LOCATE 2,7:PRINT "欢迎光临";
5902 LOCATE 3,7:PRINT "传送中心";:LOCATE 4,7:PRINT CQ;"元/次";:R$=INKEY$
5905 IF G<CQ THEN CLS:LOCATE 3,6:PRINT "你的钱不够";:R$=INKEY$: GOTO 1505
5909 CLS:LOCATE 2,7:PRINT "1.";CS$(0);
5910 LOCATE 3,7:PRINT "2.";CS$(1);:LOCATE 4,7:PRINT "3.取消";
5920 R=ASC(INKEY$):IF R=98 THEN R=0 ELSE IF R=110 THEN R=1 ELSE IF R=109 THEN 6000
5925 IF R>2 THEN 5920
5930 G=G-CQ:N=CD(R):X=3:Y=3: GOTO 1000
6000 GOTO 1505
6010 CLS:TEXT :POKE 914,1:POKE 949,255
6012 FOR QQ=1 TO S:INPUT #2,TN$,TT$
6015 FOR R=704 TO 859:POKE R,0:NEXT :orz=26
6017 locate 1,1:print tn$,tt$;:poke 802,62:poke 803,62:print
6052 R=ASC(INKEY$):NEXT 
6055 POKE 914,0:GRAPH:RETURN
6200 CLS:LOCATE 3,6:PRINT "Loading...";:CF=0:OPEN "ST"+STR$(QJ) FOR INPUT AS#2: GOTO 7250
6205 INPUT #2,R:ON R GOTO 6230,6250
6230 INPUT #2,S:GOSUB 6010:GOSUB 6268
6240 INPUT #2,FA,FB,FC:CLOSE #2:QJ=QJ+1
6241 IF QJ=4 THEN TS(0)=1:TS(1)=1
6242 IF QJ=11 THEN TS(0)=2
6243 IF QJ=21 THEN TS(1)=2
6244 IF QJ=32 THEN TS(0)=3:TS(1)=3
6245 IF QJ=43 THEN  GOTO 5
6248 GOTO 1505
6250 DIE=1:INPUT #2,S:GOSUB 6010:INPUT #2,S
6260 FOR RT=1 TO S:INPUT #2,DH$,DT$,DC,DD,DHM,DJY,DG:Q=1:SY=0:DHP=DHM
6265 GOSUB 4608:NEXT RT:DIE=0:GOSUB 6268: GOTO 6280
6268 INPUT #2,S:IF S=0 THEN 6278
6270 FOR RT=1 TO S:INPUT #2,R,R$,Q,T
6271 IF ZS(R)>9 THEN  GOTO 6276
6272 CLS:LOCATE 3,9-LEN(R$)/2:PRINT "获得";R$;
6274 ZS(R)=ZS(R)+1:WP$(R,ZS(R))=R$:WP(R,ZS(R),0)=Q:WP(R,ZS(R),1)=T
6276 R$=INKEY$:NEXT 
6278 RETURN
6280 INPUT #2,S:GOSUB 6010: GOTO 6240
6800 CLS:LOCATE 3,6:PRINT "Loading...";:OPEN "SA" FOR OUTPUT AS#1
6810 WRITE #1,"太乐","月萧","","","猪派弥乐神功","猪派月舞神功","灵云神掌"
6815 WRITE #1,"幽灵穿心剑","火云爆焰拳","棒棒糖二刀斩"
6820 FOR Q=0 TO 1:WRITE #1,WQ$(Q),ZB$(Q):NEXT 
6850 FOR R=0 TO 3: FOR S=1 TO 10:WRITE #1,WP$(R,S):NEXT S,R
6900 CLOSE #1:OPEN "SA" FOR INPUT AS#1
6914 INPUT #1,H$(0),H$(1),HT$(0),HT$(1)
6917 FOR R=1 TO 3:INPUT #1,TJ$(0,R),TJ$(1,R):NEXT 
6920 FOR Q=0 TO 1:INPUT #1,WQ$(Q),ZB$(Q):NEXT 
6925 FOR R=0 TO 3: FOR S=1 TO 10:INPUT #1,WP$(R,S):NEXT S,R:CLOSE #1
6930 GOTO 1002
7000 CLS:IF TS(Q)<1 THEN 7100
7015 LOCATE 3,3:PRINT "            ";:T=1
7020 LOCATE 3,5:PRINT "            ";:LOCATE 3,11-LEN(TJ$(Q,T))/2:PRINT TJ$(Q,T);
7030 R=ASC(INKEY$)
7040 IF R=22 AND T<TS(Q) THEN T=T+1: GOTO 7020 
7042 IF R=23 AND T>1 THEN T=T-1: GOTO 7020 
7045 IF R=13 THEN 7060 
7046 IF R=27 THEN 7110 
7055 GOTO 7030
7060 CLS:LOCATE 3,16:PRINT DT$;
7065 ON T GOTO 7205,7800,7900
7100 LOCATE 3,7:PRINT "暂无特技";:R$=INKEY$
7110 Q=1-Q: GOTO 4608
7200 IF MP(Q)<CC THEN CLS:LOCATE 3,8:PRINT "MP不足";:R$=INKEY$:Q=1-Q:CC=0
7202 RETURN
7205 CC=3:GOSUB 7200:IF CC=0 THEN 4608
7206 ON Q+1 GOTO 7210,7220,8000
7210 FOR R=0 TO 30 STEP 0.2:CIRCLE 30,40,R,1,1:NEXT 
7214 FOR R=30 TO 160:LINE R,25,R,55,1:NEXT 
7216 FOR R=1 TO 1200:NEXT 
7217 BB=(A(Q)/10+0.5+RND(1)/2)*45:IF QJ>6 THEN BB=BB*3/2
7218 IF A(Q)>10 THEN BB=BB+(A(Q)-10)*3.8
7219 GOTO 7110
7220 FOR R=28 TO 140 STEP 4
7221 CIRCLE R,40,20,1,1:CIRCLE R-8,40,20,1,0
7222 FOR T=1 TO 150:NEXT T
7223 NEXT R
7232 GOTO 7216
7250 INPUT #2,R,A$:ON R GOTO 7260,7270,7280
7260 GOTO 6205
7270 INPUT #2,S: FOR R=1 TO S:INPUT #2,T,T,T,T:NEXT :INPUT #2,T,R$,T,T: GOTO 7260
7280 INPUT #2,R$,T: GOTO 7260 
7300 R$="ST"+STR$(M+(N-1)/2*5):CLS:OPEN R$ FOR INPUT AS#2:INPUT #2,R,R$
7310 LOCATE 1,1:PRINT CHR$(252);CHR$(L);":";:LOCATE 2,4:PRINT R$;:R$=INKEY$
7320 ON R GOTO 7330,7340,7490
7330 XX=X:YY=Y:CLOSE #2: GOTO 1505
7340 INPUT #2,S:YK=1
7350 FOR R=1 TO S:INPUT #2,XH,QQ,SY,ZD:WS(R)=QQ:Q=0:T=0
7360 WHILE Q<QQ AND T<ZS(XH)
7370 T=T+1:IF WP(XH,T,0)=SY AND WP(XH,T,1)=ZD THEN Q=Q+1:IF Q=1 THEN WM$(R)=WP$(XH,T)
7375 WEND
7380 IF Q<QQ THEN YK=0 
7385 NEXT R
7386 IF YK=0 THEN 7330
7387 INPUT #2,XH,A$,Q,T:CLS
7390 IF ZS(XH)>9 THEN 7330
7393 LOCATE 1,1:PRINT "用";: FOR R=1 TO S:RT=INT((R+1)/2):QQ=(R-(RT-1)*2-1)*10+1:RT=RT+1
7394 LOCATE RT,QQ:PRINT WS(R);WM$(R);:NEXT :PRINT :PRINT "换";:PRINT A$;:PRINT "?(Y/N)";
7395 R$=INKEY$:IF R$="y" THEN  GOTO 7396 ELSE IF R$="n" THEN  GOTO 7330 ELSE  GOTO 7395
7396 CLS:LOCATE 3,9-LEN(A$)/2:PRINT "获得";A$;:ZS(XH)=ZS(XH)+1:WP$(XH,ZS(XH))=A$
7406 WP(XH,ZS(XH),0)=Q:WP(XH,ZS(XH),1)=T:CLOSE #2:OPEN "ST"+STR$(M+(N-1)/2*5) FOR INPUT AS#2
7416 INPUT #2,R,R$,S
7426 FOR R=1 TO S
7436 INPUT #2,XH,Q,SY,ZD:T=0
7446 WHILE Q>0
7448 T=T+1
7456 IF WP(XH,T,0)<>SY OR WP(XH,T,1)<>ZD THEN  GOTO 7480
7460 FOR RT=T TO ZS(XH)-1:WP$(XH,RT)=WP$(XH,RT+1):WP(XH,RT,0)=WP(XH,RT+1,0)
7470 WP(XH,RT,1)=WP(XH,RT+1,1):NEXT RT:T=T-1:Q=Q-1:ZS(XH)=ZS(XH)-1
7480 WEND:NEXT : GOTO 7330 
7490 CLS:INPUT #2,R$,S:TEXT :LOCATE 2,4:PRINT "输入你的答案:":LOCATE 3,5:INPUT RR$
7497 GRAPH:CLS
7498 IF RR$<>R$ THEN LOCATE 3,7:PRINT "你答错了";: GOTO 7510
7500 LOCATE 2,7:PRINT "你答对了";
7505 IF G<800 AND QJ<7 THEN LOCATE 3,5:PRINT "奖励金钱";S;:G=G+S
7510 R$=INKEY$: GOTO 7330
7600 CLS:LOCATE 2,8:PRINT "读取进度";:LOCATE 3,7:LOCATE 3,8:PRINT "存储进度";
7610 LOCATE 4,8:PRINT "退出游戏";:T=S
7620 LOCATE T,6:PRINT "  ";:LOCATE S,6:PRINT "";:T=S
7630 R=ASC(INKEY$)
7635 IF R=21 AND S<4 THEN S=S+1: GOTO 7620
7640 IF R=20 AND S>2 THEN S=S-1: GOTO 7620
7646 IF R=27 THEN 1505
7647 IF R=13 THEN 7665
7650 GOTO 7630
7665 IF S=2 THEN 7730
7668 IF S=3 THEN 7670
7669 IF S=4 THEN POKE 199,255
7670 GOSUB 7760:IF R=0 THEN 7600
7681 OPEN "JD"+STR$(R) FOR OUTPUT AS#1:WRITE #1,N,M,QJ,CF,X,Y,G,TS(0),TS(1),FA,FB,FC
7682 FOR Q=0 TO 1:WRITE #1,A(Q),B(Q),HP(Q),MP(Q),WQ$(Q),UC(Q),UE(Q),ZB$(Q),VD(Q),VF(Q):NEXT 
7692 FOR R=0 TO 3:WRITE #1,ZS(R)
7702 FOR S=1 TO 10:WRITE #1,WP$(R,S),WP(R,S,0),WP(R,S,1):NEXT S,R
7712 CLOSE #1:CLS:LOCATE 3,7:PRINT "存储完毕";:R$=INKEY$: GOTO 1505
7730 GOSUB 7760:IF R=0 THEN ON S-1 GOTO 7600,305
7739 OPEN "JD"+STR$(R) FOR INPUT AS#1:INPUT #1,N,M,QJ,CF,X,Y,G,TS(0),TS(1),FA,FB,FC
7740 FOR Q=0 TO 1:INPUT #1,A(Q),B(Q),HP(Q),MP(Q),WQ$(Q),UC(Q),UE(Q),ZB$(Q),VD(Q),VF(Q):NEXT 
7742 FOR R=0 TO 3:INPUT #1,ZS(R)
7745 FOR S=1 TO 10:INPUT #1,WP$(R,S),WP(R,S,0),WP(R,S,1):NEXT S,R
7750 CLOSE #1 : GOTO 1000
7760 CLS:LOCATE 2,6:PRINT "1.存 档 一";:LOCATE 3,6:PRINT "2.存 档 二";
7762 LOCATE 4,6:PRINT "3.存 档 三";:R=0
7763 R$=INKEY$:IF R$="b" THEN R=1 ELSE IF R$="n" THEN R=2 ELSE IF R$="m" THEN R=3
7764 IF ASC(R$)=27 THEN 7769
7765 IF R=0 THEN  GOTO 7763
7768 CLS:LOCATE 3,6:PRINT "Loading...";
7769 RETURN
7800 CC=1+INT(A(Q)/2):GOSUB 7200:IF CC=0 THEN 4608
7810 ON Q+1 GOTO 7820,7855
7820 FOR R=30 TO 10 STEP -1:LINE 10,R+1,30,R+1,0:BOX 10,R,30,R-10,1:LINE 10,79-R,30,79-R,0
7830 BOX 10,80-R,30,90-R,1: FOR S=1 TO 100:NEXT S,R 
7840 FOR R=10 TO 149: FOR S=20 TO 60 STEP 10:CIRCLE R,S,5,1,2:NEXT S,R 
7850 BB=RND(1)*100+250+INT(A(Q)/15)*150: GOTO 7110
7855 FOR R=1 TO 78 STEP 18: FOR S=16 TO 144 STEP 3:BOX S-16,R,S-1,R+4,1,0:BOX S,R,S+15,R+4,1
7856 NEXT S,R
7857 FOR R=1 TO 200:LINE RND(1)*99+43,RND(1)*79,RND(1)*99+43,RND(1)*79,2:NEXT 
7858 BB=C(Q)*4+100+RND(1)*200: GOTO 7110
7860 IF DIE=1 THEN CLOSE #2
7865 OPEN "DI" FOR INPUT AS #2:S=13:GOSUB 6010:CLOSE #2:DIE=0: GOTO 5
7900 CC=MP(Q):IF CC=0 THEN CLS:LOCATE 3,8:PRINT "MP不足";:R$=INKEY$:Q=1-Q: GOTO 4608
8000 ON Q+1 GOTO 8100,8200
8100 LOCATE 3,1:PRINT "": FOR R=24 TO 158:CIRCLE R,37,2,1,3: FOR S=1 TO 10:NEXT S,R
8110 LOCATE 3,1:PRINT "": FOR R=24 TO 158:CIRCLE R,40,5,1,3: FOR S=1 TO 10:NEXT S,R
8120 LOCATE 3,1:PRINT "": FOR R=29 TO 158:CIRCLE R,40,10,1,3: FOR S=1 TO 10:NEXT S,R
8125 FOR RR=1 TO 100:R=RND(1)*80+80:S=RND(1)*80:CIRCLE R,S,10,1,3:NEXT 
8130 BB=MP(Q)*(35+RND(1)*15)
8140 GOTO 7110
8200 FOR R=14 TO 149 STEP 27:CIRCLE R,25,10,1,3:BOX R-1,35,R+1,55,1: FOR S=1 TO 500:NEXT S,R
8210 FOR R=3 TO 78:S=140-0.8*(R-3):BOX S,R,S+10-(R-3)*2/15,R,1,2:NEXT 
8220 FOR R=3 TO 78:S=80+0.8*(R-3):BOX S,R,S+10-(R-3)*2/15,R,1,2:NEXT 
8229 FOR R=1 TO 2000:NEXT 
8230 GOTO 8130